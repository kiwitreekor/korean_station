define(_stringid, 0)
define(setstringid, {define({$1}, _stringid) define({_stringid}, incr(_stringid))})
define(setlangid, {ifdef({_langid}, {define({_langid}, {$1})}, {define({_langid}, {ALL})})})
define(defgrftextlang, {ifdef({$1}, {defgrftext($1,defn({_langid}),{UTF8 }$2)}, {defgrftext(_stringid,ALL,{UTF8 }$2) setstringid({$1})})})

define(_append, {define({$1},defn({$1}){, $2})})
define(defstr, {ifdef({$1}, {_append({$1},defn({_langid}){, UTF8 $2})}, {define({$1},{ALL, UTF8 $2})})})

define({show}, {{$1}
})

define({file_num}, 0)

ifdef({process_act2},,{
include(m4_ref)
define({refloc}, {ifdef(format({{refloc[%s]}}, {$1}), {defn(format({{refloc[%s]}}, {$1}))}, {0})})

define({__xdef}, defn({def}))
define({__xref}, defn({ref}))
define({__xmakestation}, defn({makestation}))

define({join}, {ifelse({$#}, {2}, {{$2}}, {ifelse({$2}, {}, {}, {{$2}_})$0({$1}, shift(shift($@)))})})
define({_join}, {ifelse({$#$2}, {2}, {}, {ifelse({$2}, {}, {}, {{$1$2}})$0({$1}, shift(shift($@)))})})

define({_popfront}, {dnl
define({tmp_str}, $1)dnl
define({tmp_loc}, index(tmp_str, {|}))dnl
ifelse(eval(tmp_loc==-1),1,{dnl
tmp_str{}dnl
define({$1},{})dnl
},{dnl
substr(tmp_str, 0, tmp_loc)dnl
define({$1}, substr(tmp_str, eval(tmp_loc+1), eval(len(tmp_str)-tmp_loc-1)))})dnl
undefine({tmp_loc})dnl
undefine({tmp_str})dnl
})

define({_peekfront}, {dnl
define({tmp_str}, $1)dnl
define({tmp_loc}, index(tmp_str, {|}))dnl
ifelse(eval(tmp_loc==-1),1,{dnl
tmp_str{}dnl
},{dnl
substr(tmp_str, 0, tmp_loc)dnl
})dnl
undefine({tmp_loc})dnl
undefine({tmp_str})dnl
})


define({foreachi}, {dnl
define({tmp_2}, $2)dnl
define({tmp_1}, _popfront({tmp_2}))dnl
ifelse(tmp_1, {}, {}, {define({$1}, tmp_1){}$3}{$0({$1},tmp_2,{$3})})dnl
})

define({free_act2_ids}, {})
forloop({i}, {define({free_act2_ids}, join({|}, free_act2_ids, i))}, 0 .. 254)

define({check_free_act2_id}, {dnl
ifdef({act2_list_all}, {dnl
foreachi({i}, act2_list_all, {dnl
ifdef(format({{act2_list_%d}}, i), {dnl
ifelse(eval(cur_loc > act2_list(i)), 1, {dnl
define({tmp_list}, free_act2_ids)dnl
define({free_act2_ids}, join({|}, tmp_list, i))dnl
act2_list_remove(i)dnl
})dnl
})dnl
})dnl
})dnl
})

define({get_free_act2_id}, {dnl
check_free_act2_id()dnl
ifelse($1, 0, {dnl
_peekfront({free_act2_ids})dnl
},{dnl
_popfront({free_act2_ids})dnl
})dnl
})

define({act2_list}, {defn(format({{act2_list_%d}}, {$1}))})
define({act2_list_add}, {
	ifdef(format({{act2_list_%d}}, {$1}),,{ifdef({act2_list_all}, {define({act2_list_all}, join({|}, act2_list_all, $1))}, {define({act2_list_all}, $1)})})
	define(format({{act2_list_%d}}, {$1}), {$2})
})
define({act2_list_remove}, {dnl
ifdef(format({{act2_list_%d}}, $1),{dnl
define({tmp_list}, {})dnl
foreachi({i}, act2_list_all, {dnl
ifelse(eval(i==$1), 1, {}, {dnl
define({tmp_list}, join({|}, tmp_list, i))dnl
})dnl
})dnl
})dnl
define({act2_list_all}, tmp_list)dnl
undefine(format({{act2_list_%d}}, $1), {})dnl
})

define({cur_loc}, 0)
define({incr_loc}, {define({cur_loc}, incr(cur_loc))})

define({def}, {dnl
ifdef({$1}, {__xdef($1)}, {dnl
define({tmp_ref}, refloc({$1}))dnl
define({tmp_id}, get_free_act2_id(tmp_ref))dnl
act2_list_add(tmp_id, tmp_ref) define({$1}, tmp_id)dnl
changecom({/*},{*/})dnl
// {$1}: tmp_id
changecom({//})dnl
__xdef(tmp_id)dnl
})dnl
incr_loc{}dnl
})dnl

define({makestation}, defn({incr_loc})defn({__xmakestation}))
})

define({defstationname}, {stationnames($1, {_langid, UTF8 {$2}})})
define({defclassname}, {classnames($1, {_langid, UTF8 {$2}})})

define(tinfo_grfid, {89 6A $1 00 FF FF FF FF __byte(__count)
__postprocess(2,$2) define({__count},0) define({__wordflag}, 0)})